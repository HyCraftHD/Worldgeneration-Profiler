buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.9"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"

ext.buildProps = file "build.properties"

buildProps.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = config.mod.version
archivesBaseName = config.mod.modid

sourceCompatibility = targetCompatibility = "1.8"

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = config.forge.version
    runDir = "run"
    mappings = config.forge.mappings
    makeObfSourceJar = false
    
    replace "@VERSION@", config.mod.version
}

jar {
    
     from "LICENSE"
    
    archiveName = archivesBaseName + "-v" + version + ".jar"
    
    manifest.mainAttributes(
       	"Implementation-Title": config.mod.name,
    	"Implementation-Version": version,
		"Built-On": minecraft.version
	)
	
	manifest {
        attributes("FMLCorePlugin": "net.hycrafthd.worldgenerationprofiler.WorldGenerationProfilerPlugin")
    }

}

dependencies {
}

processResources {
	from(sourceSets.main.resources.srcDirs) {
        exclude "*"
	}
}

task signJar(type: SignJar, dependsOn: reobfJar) {

    keyPass = findProperty("signJar.keyPass")
    keyStore = findProperty("signJar.keyStore")
    storePass = findProperty("signJar.storePass")
    alias = findProperty("signJar.alias")

    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar


curseforge {
    apiKey = project.properties["token"]
    
    tasks.curseforge.dependsOn build
    
    project {  
        id = config.curse.id
        releaseType = config.curse.type

        addGameVersion "1.8"
        addGameVersion "1.8.8"
        addGameVersion "1.8.9"
        addGameVersion "1.9"
        addGameVersion "1.9.4"
        addGameVersion "1.10"
        addGameVersion "1.10.2"
        addGameVersion "1.11"
        addGameVersion "1.11.2"
        addGameVersion "1.12"
        addGameVersion "1.12.1"
        addGameVersion "1.12.2"

        mainArtifact(jar) {
            displayName = config.curse.display + "-" + version
        }
    }
}
